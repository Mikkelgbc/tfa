---
output: html_document
editor_options: 
  chunk_output_type: console
---

```{r, code = readLines("common.R"), cache = FALSE, include=FALSE}
```

```{r, include=FALSE}
module_number_prefix <- "05"
module_number <- as.numeric(module_number_prefix)
module_name <- "vba-datatypes"
here::i_am(str_c("book/", module_number_prefix, "_", module_name, ".Rmd"))
# module_name_underscore <- str_replace_all(module_name, "-", "_")
sheet_name_prefix <- str_c("TM", module_number)
```


# Advanced data types and usage {#mod-vba-datatypes}

This module gives a short introduction to some of the more advanced data types. 

#### Learning path diagram {-}

Click/hover the nodes to follow links and see details.

```{r, echo=FALSE, out.width="100%", fig.asp=NA}
g <- create_learning_path(
   url = "https://docs.google.com/spreadsheets/d/1bBe42LHK-bE7CsU9eNBzi_7VNjbmv-Ybr7285pE61jM/edit?usp=sharing", 
   sheet = "vba-datatypes", 
   x_legend = 3.5, y_legend = 4.5)
render_graph(g, height = 400)
```

## Learning outcomes {#lo-vba-datatypes}

By the end of this module, you are expected to be able to:

* Describe what a variable is.
* Name different data types and how they effect memory size.
* Declare a variable as a data type.
<!-- * Describe that scope for a variable means. -->
* Declare and use an array with both fixed and dynamic dimension.
* Declare and use a collection.
* Assign values to variables.
<!-- * Formulate well-structured code. -->

The learning outcomes relate to the [overall learning goals](#lg-course) number 1, 2, 4, 8 and 9 of the course.

<!-- SOLO increasing: identify · memorise · name · do simple procedure · collect data · -->
<!-- enumerate · describe · interpret · formulate · list · paraphrase · combine · do -->
<!-- algorithms · compare · contrast · explain causes · analyse · relate · derive · -->
<!-- evaluate · apply · argue · theorise · generalise · hypothesise · solve · reflect -->

Recall that variables are used to store information that is saved in memory. A variable may store different data types such as an integer, a double, a string or an object (a group of variables). 



You may visualize a variable as a box in memory (see Figure \@ref(fig:vba-memory)). The variable name can be seen as the label on the box. The box can contain for instance a number, a date or a boolean. That is, any data type defined by VBA. Some of the *basic data types* used by VBA is:

Name    | Type      | Details                                                                        
------- | --------- | -------------------------------------------------------------------------------
Byte    | Numerical | Whole number between 0 and 255.                                                
Integer | Numerical | Whole number between -32'768 and 32'767.                                       
Long    | Numerical | Whole number between - 2'147'483'648 and 2'147'483'647.                        
Double  | Numerical | Floating decimal number between -1.79769313486232E308 and 1.79769313486232E308.
String  | Text      | Text.                                                                          
Date    | Date      | Date and time.                                                                 
Boolean | Boolean   | True or False.                                                                 
Variant | Any type  | Any kind of data (default type if the variable is not declared). 

All basic data types can be seen in the [VBA documentation](https://docs.microsoft.com/en-us/office/vba/language/reference/user-interface-help/data-type-summary). However, this course will focus on the ones stated above. Note that some data types are numericals, i.e. they represent a number (either an integer or a decimal number), other data types represent a set of characters (a string), a boolean or a date. Variables can also contain *advanced data types* such as a group of integers (an array), a range of cells in a worksheet (an object) or a set of numbers (a collection). We will consider advanced data types in Sections ??-??.

Always declare your variables explicit in VBA. This is good coding practice even though you do not have to do it. You can add `Option Explicit` in the top of your module. This ensures that undefined variables raise an error. You can add it by default by modifying the preferences for the VBA editor. Open the preferences/options **Excel -> Preferences -> Editor** (`r icons::fontawesome$brands$apple`) or **Tools -> Options -> Editor** (`r icons::fontawesome$brands$windows`) and check mark 'Require Variable Declaration'. Now the option is added automatically when you add a new module.


## Why declare variables? 

Your memory contains a limited amount of storage and it is therefore important to use it wisely. The computer memory can be seen as a group of bits (zero and ones) and we can measure the memory size by counting the number of bits. Often we use other measures such as: 8 Bits = 1 Byte, 1024 Bytes = 1 Kilobyte, 1024 Kilobytes = 1 Megabyte, 1024 Megabytes = 1 Gigabyte and 1024 Gigabytes = 1 Terabyte. Different data types take up different amounts of memory. For example, the memory requirements for some of the basic data types are:









Data type                                 | Storage size            
----------------------------------------- | -------------------------
Byte                                      | 1 byte                  
Boolean                                   | 2 bytes                 
Integer                                   | 2 bytes                 
Long  (long integer)                      | 4 bytes                 
Double  (double-precision floating-point) | 8 bytes                 
Date                                      | 8 bytes                 
String                                    | 10 bytes + string length * 2 bytes
Variant  (a number)                       | 16 bytes                
Variant  (a string)                       | 22 bytes + string length * 2 bytes

<!-- Arrays require 20 bytes of memory plus 4 bytes for each array dimension plus the number of bytes occupied by the data itself. A Variant containing an array requires 12 bytes more than the array alone. -->

As can be seen a double takes 4 times the memory compared to an integer. That is, you can save memory by considering what data type you need. Consider an example where you have 10000 customer locations on a map and you want to store the distance between customer $i$ and $j$. That is, you have to store $10000 \cdot 10000 = 100.000.000$ numbers. The memory 
requirements given different data types are:

Data type | Memory requirements
-------- | -------------------------------------------------------
Variant  | $(10000\cdot 10000\cdot 16)/1024/1024 \approx 1526$ MB 
Double   | $(10000\cdot 10000\cdot 8)/1024/1024 \approx 763$ MB                                     
Integer  | $(10000\cdot 10000\cdot 2)/1024/1024 \approx 191$ MB  

If you do not think about memory usage a Variant data type would have been used taking up the double the size compared to using a Double (a decimal number). Moreover, if it is enough to measure the distance using an Integer between 0 and 32767, then we can reduce the memory requirements to only 191 MB. Often the free memory in your computer is around 5 GB, i.e. think about which data type you need! 

Another reason for declaring variables is that you avoid errors such as:

```
Option Explicit

Dim intCtr as integer

intCtr = 10
intCtr = intCtg + 10
```

This will raise an error because `intCtg` is not defined (you have made a typo and meant `intCtr`). Without `Option Explicit` the code will run and assume that `intCtg` is another variable (initialized to zero).


## Declare and assign values to variables 

You declare variables using the `Dim` keyword: 

```
'' Declare some variables
Sub TM03_DeclareVariables()
    ' Always declare variables in the top of a procedure (memory is allocated)
    Dim intPersons As Integer
    Dim dblAmount As Double
    Dim strText As String
    
    ' Here we assign values to the variables (modify the memory)
    intPersons = 10
    dblAmount = 27.4
    strText = "Number of persons are "
    
    MsgBox (strText & intPersons & " which own " & dblAmount & "$")
End Sub
```

Declaration is done in the top of the procedure (good coding practice) and we assign values to the variables afterwards. 

Note VBA code is case-insensitive, i.e. `strText` and `strtext` is the same variable. It is good coding practice to be consistent and often the VBA editor will help you by changing `strtext` to `strText` automatically.



<!-- ## Scope of variables  -->

<!-- This is dropped (we do not use any global variables) -->

## Strings

Strings contain a group of characters (an empty string is of length zero). Memory requirements vary with the length of the string (10 bytes + length $\cdot$ 1 byte). We use the symbol `&` to concatenate strings (glue strings together):

```
'' Concatenate two strings
Sub TM03_StrConcat()
    Dim strX As String
    Dim strY As String
    Dim strRes As String
    
    strX = "VBA"
    strY = "Strings"
    strRes = strX & " " & strY
    MsgBox (strRes)
    MsgBox (strX & vbCr & strY)   ' use vbCr to insert a new line
End Sub
```

Note you can use constant `vbCr` to add a new line.

There are many [VBA functions that can be used to manipulate strings](https://docs.microsoft.com/en-us/dotnet/visual-basic/language-reference/functions/string-functions). Some examples:

```
'' String functions
Sub TM03_StrFunc()
    Dim str As String
    
    str = "VBA Strings"
    MsgBox ("The length is: " & Len(str))
    MsgBox ("In lowercase: " & LCase(str))
    MsgBox ("Last 7 char: " & Right(str, 7))
    MsgBox ("Replace: " & Replace(str, "Strings", "Rules"))
    MsgBox ("Compare: " & StrComp(str, "VBA Strings"))  ' result is 0 (equal)
    MsgBox ("Compare: " & StrComp(str, "Apple"))        ' result is 1 (str alfabetically after)
    MsgBox ("Compare: " & StrComp(str, "Wait"))         ' result is -1 (str alfabetically before)
End Sub
```


## Objects {#vba-objects}

VBA have a lot of predefined objects you can use. Think of an object as a datatype that holds a group of variables. Examples of some objects are `Range`, `Worksheet`, and `WorksheetFunction`. Objects are grouped, nested and you refer to an object by specifying the path, e.g.:

```
Workbooks("Jobs").Worksheets("Data values").Range("D4").value
```

Here we refer to the value in cell D4 in worksheet *Data values* in the file *Jobs*. You may skip parts of the path (VBA then uses the current active one):

```
Workbooks("Jobs").Worksheets("Data values").Range("D4").value  ' full specification
Worksheets("Data values").Range("D4").value  ' same result if Excel file "Jobs" is active
Range("D4")  ' same result if Excel sheet "Data values" is active and "Jobs" file
```

Note `.value` has been dropped in the last line because it is the default, i.e. you do not have to write it explicit. 

Object variables are declared like any other variables:

```
Dim rng As Range
Dim wst As Worksheet
```

Use `Set` to allocate the Object:

```
Set rng = Range("F7")
Set wst = Worksheets("Data values")
```

Now `rng` is a reference to cell F7 and `wst` is a reference to worksheet *Data values*. Think of a reference as a value that identify where in memory the object is. 

<!-- You may declare and set in one go using the `New` keyword: -->

<!-- ``` -->
<!-- Dim col as New Collection  ' col is now an empty collection (0 items) -->
<!-- ``` -->

In the next sections let us have a look at some relevant objects.


## Worksheets

The `Worksheet` object refer to a worksheet in the Excel file and you can use it to e.g. modify cells:

```
Sub TM5_TestWorksheet()
   ThisWorkbook.Activate  ' activate this workbook
   
   Worksheets("TM5_Test1").Range("B2") = "Testing Worksheet"  ' write to cell in sheet TM5_Test1
   Sheet_TM5_Test1.Range("B3") = "Use the sheets code name"   ' use the code name (also work if sheet renamed)
   'ThisWorkbook.Worksheets("Test3").Range("B5") = "Test"     ' Error since there is no sheet Test3 in this workbook
   Range("B4") = "Do you know which sheet is active?"         ' Active sheet
   Worksheets("TM5_Test1").Activate  ' Make sheet active so know where is (good coding pratice)
   Range("B5") = "I know which sheet is active!"
End Sub
```

It is always good practice to make sure that you are working with the correct Excel file. Otherwise you will get errors if another file is active. This can be done using the `ThisWorkbook` object. Similar you either refer to a worksheet directly using its name or its code name. The code name can be set in the Properties window in the VBA editor. An alternative is to activate the worksheet. 

You can use `Worksheet` variables:

```
Sub TM5_TestWorksheetVar()
  Dim wst1 As Worksheet  ' define a variable which hold a reference to a Worksheet object
  Dim wst2 As Worksheet  ' define a variable which hold a reference to a Worksheet object
  Dim rng As Range
  
  Set wst1 = ThisWorkbook.Worksheets("TM5_Test1")  ' set the reference
  Set wst2 = ThisWorkbook.Worksheets("TM5_Test2")  ' set the reference
  wst1.Range("B6") = "Writing using wst1"
  wst2.Range("B2") = "Writing using wst2"
  wst1.Activate   ' just to have a look
End Sub
```

Here by having two worksheet variables we can write directly to cells in different worksheets without activating the worksheet.

You may check if a worksheet exists using:

```
'' Check if a worksheet exists
' @param strName Name of worksheet.
' @return True if exists.
Function WstExists(strName As String) As Boolean
    WstExists = Evaluate("ISREF('" & strName & "'!A1)")
End Function
```

The worksheet object has a lot of *methods/properties* (think of methods as procedures defined inside the object) for instance `wst.Add` and `wst.Delete`. Let us try to define a function that delete a worksheet:

```
'' Delete a worksheet if it exists
' @param strName Name of worksheet.
' @return True if deleted.
' @author Lars Relund <lars@relund.dk>
Function WstDelete(strName As String) As Boolean
    Dim wst As Worksheet
    Dim bln As Boolean
    
    bln = Application.DisplayAlerts
    Application.DisplayAlerts = False   ' no "really want to delete" alert
    If WstExists(strName) Then
        Worksheets(strName).Delete
        WstDelete = True
    Else
        WstDelete = False
    End If
    Application.DisplayAlerts = bln     ' restore value
End Function
```

We first use the function `WstExists` to check if there is a worksheet. If there is, we call the `.delete` method and delete the worksheet. Note since we do not want an alert stating if we really want to delete the worksheet this is disabled using the `Application` object. The function returns true if the worksheet has been deleted.

Let us try to define a make a function that create a worksheet:

```
'' Create a worksheet
' @param strName Name of worksheet.
' @param blnForce Force deletion of worksheet if exists.
' @return True if created.
Function WstCreate(strName As String, Optional blnForce As Boolean = False) As Boolean
    Dim wst As Worksheet
    
    If blnForce And WstExists(strName) Then Call WstDelete(strName)
    If Not WstExists(strName) Then
        Set wst = Worksheets.Add
        wst.Name = strName
        WstCreate = True
    Else
        WstCreate = False
    End If
End Function
```

We use an optional argument to force deletion of the old worksheet (if a worksheet with the same name). Next, we create the worksheet using the `.Add` method and rename it using the `.Name` method. The function returns true if the worksheet has been created.

Finally, let us create a function that clear a worksheet:

```
'' Clear a worksheet if it exists
' @param strName Name of worksheet.
' @param blnCells Delete cell contents, formats, comments, etc. (default).
' @param blnContents Delete cell contents.
' @param blnFormat Delete cell format.
' @param blnObjects Delete cell buttons and charts.
' @return True if cleared.
Function WstClear(strName As String, _
  Optional blnCells As Boolean = True, _
  Optional blnContents As Boolean = False, _
  Optional blnFormat As Boolean = False, _
  Optional blnObjects As Boolean = False) As Boolean
    Dim wst As Worksheet
    
    On Error Resume Next
    If WstExists(strName) Then
        Set wst = Worksheets(strName)
        If blnCells Then wst.UsedRange.Clear
        If blnContents Then wst.Cells.ClearContents
        If blnFormat Then wst.Cells.ClearFormats
        If blnObjects Then
            wst.ChartObjects.Delete
            wst.Buttons.Delete
        End If
        WstClear = True
    Else
        WstClear = False
    End If
End Function
```

First note that we have a lot of optional arguments depending on what we want to clear. Next the `On Error Resume Next` statement is used to make the program not stop even if an error happens. Finally, depending on the setting of the optional arguments we e.g. clear the format in all cells if `blnFormat` is true.

All the functions above is part of the *[course procedures](#vba-course-procedures)* that you may use 'as is' during the course and at the exam. I will explicitly state if you are not allowed to use them otherwise. All the worksheet procedures start with `Wst` so you easy can find them using auto complete in the VBA editor. All worksheet procedures are stored in the `ModWst` module. You may open the module to have a look at the procedures.

Let us see them in action:

```
'' Test the worksheet functions
Sub TM5_TestWorksheetFunc()
    If WstCreate("Test", blnForce:=True) Then MsgBox ("Created Test")
    If WstRename("Test", "Test1") Then MsgBox ("Renamed the Test to Test1")  ' only work if no Test1 sheet
    If WstClear("Test8") Then MsgBox ("Cleared Test8") ' no clearing since on sheet with that name
    If WstDelete("Test1") Then MsgBox ("Deleted Test1")
End Sub
```


### Ranges

Ranges are objects that refer to parts of a worksheet e.g. a cell, a row, a column, or a rectangular selection of cells. Ranges can be used to read and write to cells

```
Sub TM5_RangeEx1()
   Dim rng As Range
   Dim cell As Range
   Dim i As Integer
   
   Call TM5_ClearTestTM5    ' clear test cells so have empty cells
   ThisWorkbook.Worksheets("TM5").Activate
   ' Modify a range to a fixed value
   Set rng = Range("G4:I6")
   rng = 145   ' cell value in rng
   MsgBox rng.Address   ' range address ($A$1:$D$5)
   ' Use for each to scan range (direction left-down)
   Set rng = Range("G8:I10")
   i = 0
   For Each cell In rng
      cell = "Entry " & i
      i = i + 1
   Next
   ' Use Cells to set a range
   Set rng = Range(Cells(23, 3), Cells(25, 6))
   MsgBox rng.Address
End Sub
```

The method `.Address` is used to return the cell address. Similar the number of rows can by found using `.rows.Count`:

```
'' Rows in range
' @param rng A range.
Function RngGetRows(rng As Range) As Long
    RngGetRows = rng.rows.Count
End Function
```

We can also find number of columns, address of upper left cell or lower right cell etc. In the [course procedures](#vba-course-procedures) (module *ModRng*) all these have been defined. Let us try to use them:

```
Sub TM5_RangeEx2()
  Dim rng As Range
  
  Set rng = ThisWorkbook.Worksheets("TM5").Range("C4:E19")
  MsgBox ("Number of rows: " & RngGetRows(rng))
  MsgBox ("Number of cols: " & RngGetCols(rng))
  MsgBox ("First column number: " & RngGetFirstCol(rng))
  MsgBox ("First column letter: " & RngGetFirstCol(rng, asLetter:=True))
  MsgBox ("Lower right cell: " & RngGetLowerRight(rng, asString:=True))
  MsgBox ("Upper right cell: " & RngGetUpperRight(rng, asString:=True))
End Sub
```

An important method is the current region `.CurrentRegion` which expands the range until all cells surrounding the range is empty. This is very useful if don't know the size for data. Let us make a function that return the current region:

```
'' Return the current region of a range.
' @param rng The range to get the current region from.
Function RngCurRegion(rng As Range) As Range
    Set RngCurRegion = rng.CurrentRegion
End Function
```

Note we have to use the `Set` keyword since the return value is a range (object). We can now test the course procedures starting with `RngGet`:

```
Sub TM5_CurrentRegionEx1()
    Dim rng As Range

    ThisWorkbook.Worksheets("TM5").Activate
    ' Try to guess the output
    Set rng = RngCurRegion(Range("D7"))  ' rng now is the current region
    MsgBox ("Address: " & RngGetAddress(rng))
    MsgBox ("Rows = " & RngGetRows(rng) & " cols = " & RngGetCols(rng))
    MsgBox ("First row number = " & RngGetFirstRow(rng) & ". Last row number = " & RngGetLastRow(rng) & ".")
    MsgBox ("First col number = " & RngGetFirstCol(rng) & ". Last col number = " & RngGetLastCol(rng) & ".")
    MsgBox ("First col letter = " & RngGetFirstCol(rng, True) & ". Last col letter = " & RngGetLastCol(rng, True) & ".")
    ' Try to guess the output
    Set rng = RngCurRegion(Range("C23"))  ' assume we know that data contains cell C23
    MsgBox ("Address: " & RngGetAddress(rng))
End Sub
```

The same result can be obtained using the course procedures starting with `RngGetCurRegion`:

```
Sub TM5_CurrentRegionEx2()
    Dim rng As Range

    ThisWorkbook.Worksheets("TM5").Activate
    ' Try to guess the output
    Set rng = Range("D7")  ' assume we know that data contains cell D7
    MsgBox ("Address: " & RngGetCurRegionAddress(rng))
    MsgBox ("Rows = " & RngGetCurRegionRows(rng) & " cols = " & RngGetCurRegionCols(rng))
    MsgBox ("First row number = " & RngGetCurRegionFirstRow(rng) & ". Last row number = " & RngGetCurRegionLastRow(rng) & ".")
    MsgBox ("First col number = " & RngGetCurRegionFirstCol(rng) & ". Last col number = " & RngGetCurRegionLastCol(rng) & ".")
    MsgBox ("First col letter = " & RngGetCurRegionFirstCol(rng, True) & ". Last col letter = " & RngGetCurRegionLastCol(rng, True) & ".")
    
    Set rng = Range("C23")  ' assume we know that data contains cell C23
    MsgBox ("Address: " & RngGetCurRegionAddress(rng))
End Sub
```

Note the difference in how `rng` is defined. You can also copy/paste, format and clear a range using the course procedures:

```
Sub TM5_RangeEx3()
    Dim rng As Range
    Dim rngNew As Range
    
    Set rng = RngCurRegion(Range("D7"))         ' get current region
    MsgBox ("Copy to H14 (upper left corner).")
    Set rngNew = RngPaste(rng, Range("H14"))    ' rngNew is now the new range
    MsgBox ("Make yellow.")
    Call RngFormat(rngNew, "yellow")
    MsgBox ("Remove format.")
    Call RngClear(rngNew, blnCells:=False, blnFormat:=True)
    MsgBox ("Clear range.")
    Call RngClear(rngNew)
End Sub
```

Ranges can be sorted using the [`.Sort`](https://docs.microsoft.com/en-us/office/vba/api/excel.range.sort) method:

```
Sub TM5_SortRangeEx()
    Dim rng As Range
    Dim rngCur As Range
    
    ThisWorkbook.Worksheets("TM5").Activate
    Call TM5_ClearTestTM5    ' clear test cells so have empty cells
    Set rng = RngCurRegion(Range("C4"))
   
    '' Sort based on second column ascending
    Set rngCur = RngPaste(rng, Range("G4"), withFormat:=True)  ' make a copy to work with
    Call rngCur.Sort(Key1:=rngCur.Columns(2), Header:=xlYes)
    rngCur(1).Offset(-1, 0) = "Sort 2. column"  ' offset first cell in range by -1 row and 0 col

    '' Sort based on second column and afterwards 3. column (descending)
    Set rngCur = RngPaste(rng, Range("K4"), True)
    Call rngCur.Sort(Key1:=rngCur.Columns(2), Header:=xlYes, Key2:=rngCur.Columns(3), Order2:=xlDescending)
    rngCur(1).Offset(-1, 0) = "Sort 2. and next 3. column"
End Sub
```

We use the `Key` arguments to identify which columns we want to sort and the `Header` argument to identify if the first row in the range is a header. Finally the ordering is given using the `Order` argument (either xlAscending (default) or xlDescending).


## Arrays

Arrays are used to store groups of variables of a specific datatype: 

```
'' Define an array (run using the debugger - step into)
' How to check the content of an array?
' Use the Locals window together with debug mode or a message box
Sub TM5_ArrayEx1()
   Dim intAry(4) As Integer      ' define array with index 0-4
   Dim strAry(3 To 5) As String  ' define array with index 3-5
   Dim i As Integer
   
   ' Set values
   intAry(0) = 9
   intAry(1) = 12
   intAry(2) = 222
   intAry(3) = 4
   intAry(4) = 100
   ' Information about the array
   MsgBox ("Lowest index: " & LBound(intAry))
   MsgBox ("Largest index: " & UBound(intAry))
   MsgBox ("Number of elements: " & UBound(intAry) - LBound(intAry) + 1)
   MsgBox ("Array as a string: " & AryToStr(intAry))
   ' Read and assign values
    For i = 3 To 5
        strAry(i) = ThisWorkbook.Worksheets("TM5").Cells(23 + i, 3)
    Next
    MsgBox ("Array values: " & AryToStr(strAry))
End Sub
```

Array `intAry` contain 5 elements which can be accessed using index 0, 1, 2, ..., 4. In memory this is done by allocating memory for 5 integers (see Figure \@ref(fig:vba-ary)) with index 0-4. The lower and upper index bounds can be found using `LBound` and `UBound` and we can use the course procedure `AryToStr` to print it. 

```{r vba-ary, echo = FALSE, out.width="60%", fig.cap="An array in memory."} 
knitr::include_graphics("./img/array.png") 
```

Note the default start index is 0. If you want another start index you can use e.g. `Dim strAry(3 To 5) As String` which use indices 3-5 ($5-3+1$ elements). Moreover, if you want to start with index 1 as default then add `Option Base 1` to the top of your module.



### Multi-dimensional arrays

```{r vba-ary-3d, echo = FALSE, out.width="100%", fig.cap="Arrays with different dimensions."} 
knitr::include_graphics("./img/3d-array.png") 
```

.pull-left[
An array can have different dimensions (see Figure \@ref(fig:vba-ary-3d)) e.g. and array with three dimensions: 

```
Dim intOrderSize(52, 100, 50) As Integer  
```

where indices may be (week, customer, product) number. Let us assume that index start from 1 then we have an array with $52 \cdot 100 \cdot 50$ elements which can be accessed using e.g.

```
MsgBox(intOrderSize(2, 10, 20))  ' order size week 2, customer 10, product 20 
```

Let us consider an example:

```
Sub TM5_MultiDimArrayEx()
    Dim intA(20, 10) As Integer
    Dim i As Integer, j As Integer
    
    ThisWorkbook.Worksheets("TM5").Activate
    Call TM5_ClearTestTM5    ' clear test cells so have empty cells
    ' Assign some values
    For i = LBound(intA, 1) To UBound(intA, 1)
        For j = LBound(intA, 2) To UBound(intA, 2)
            intA(i, j) = WorksheetFunction.RandBetween(1, 1000)
        Next
    Next
    ' print results from G4
    For i = LBound(intA, 1) To UBound(intA, 1)
        For j = LBound(intA, 2) To UBound(intA, 2)
            Cells(i + 4, j + 7) = intA(i, j)
        Next
    Next
    ' Call AryPaste(intA, Range("G4")) ' same result
End Sub
```

We first assign random values to `intA` in the first loop. Note we use `LBound` and `UBound` to find the range of the indices (the second argument is the dimension we consider). Next, the results are printed to the sheet with upper left equal to G4. Here you may also have used the course procedure `AryPaste` instead.


### Dynamic arrays




### Use arrays instead of ranges

- Speed up code (instead of storing in a sheet)


'' Alternative (and faster) way to store a distance matrix is using an array
Sub TM3_MakeDistArray()
    Dim n As Integer
    Dim a() As Double
    Dim i As Integer
    Dim j As Integer
    
    n = Range("E1")
    ReDim a(n, n)
    For i = 1 To n
        For j = i + 1 To n
           a(i, j) = Distance(i, j)
           a(j, i) = a(i, j)
        Next
    Next
End Sub


## Collections

A useful data type not mentioned in Chapter 4 is a collection. Collections are a way of storing a group of items together (think of it as a set). Collections and arrays are both used to group variables. They both store a set of items e.g. a list of student marks or country names. If we compare collections against arrays:

- Collections are similar to arrays but better to use when the number of items is not fixed. With an array you normally set the size once. On the contrary you often add or remove items from a collection.
- Collections are better when adding and removing items.
- An item in a collection are read-only whereas an entry in an array are read/write.
- Collection can be accessed using a key or an index (starting from 1).
- Items of a collection do not have to share the same data type.

Collections are a part of the [Object Model](#mod-vba-object-model) and hence a collection have to be defined in a special way. Let us consider an [example](https://github.com/bss-osca/tfa/blob/master/vba/vba-collection-examples.xlsm). First we declare and create a collection:
```
Dim col As New Collection
```
The collection (or set) is now defined with zero items. You can add items using
```
col.Add "Apple"
col.Add "Pear"
col.Add 123
```
Let us create a function that prints the items of a collection as string:
```
Function Coll2Str(col As Collection) As String
    Dim e As Variant
    Dim str As String
    
    For Each e In col
        str = str & e & ", "
    Next e
    str = Left(str, Len(str) - 1)
    Coll2Str = str
End Function
```
Then the output of 
```
Debug.Print "The collection now contains " & col.Count & " items: " & Coll2Str(col)
```
in the Immediate window becomes
```
The collection now contains 3 items: Apple, Pear, 123
```
You can access values in the collection using index:
```
Debug.Print "The 1. item is: " & col(1)
Debug.Print "The 3. item is: " & col(3)
```
```
The 1. item is: Apple
The 3. item is: 123
```
Items are removed using:
```
col.Remove (2)
Debug.Print "The collection now contains " & col.Count & " items: " & Coll2Str(col)
```

```
The collection now contains 2 items: Apple, 123
```

Note index of items has now changed (the 3. item has become the 2. item):
```
Debug.Print "The 1. item is: " & col(1)
Debug.Print "The 2. item is: " & col(2)
```

```
The 1. item is: Apple
The 2. item is: 123
```

You clear a collection using:
```
Set col = Nothing
Debug.Print "The collection now contains " & col.Count & " items."
```

```
The collection now contains 0 items.
```

An item in a collection can be given a key:
```
col.Add "Hans Jørgensen", "ID123"   ' value, key
col.Add "Jens Hansen", "ID234"
col.Add "Lone Nielsen", "ID456"
' col.Add "Sine Mikkelsen", "ID456" ' gives an error since already used the key
Debug.Print "The collection now contains " & col.Count & " items: " & Coll2Str(col)
```

```
The collection now contains 3 items: Hans Jørgensen, Jens Hansen, Lone Nielsen
```
You can now access the item using the key:
```
Debug.Print "The item with key ID123 is: " & col("ID123")
```

```
The item with key ID123 is: Hans Jørgensen
```
Similar you can remove an item using a key:
```
 col.Remove "ID123"
 Debug.Print "The collection now contains " & col.Count & " items: " & Coll2Str(col)
```

```
The collection now contains 2 items: Jens Hansen, Lone Nielsen
```

It is recommended to use keys since using keys has three advantages:

- If the order changes your code will still access the correct item
- You can directly access the item without reading through the entire collection
- It can make you code more readable

For more information about collections you may have a look at this [webpage](http://excelmacromastery.com/excel-vba-collections/).












## How do I structure my code? {#vba-code-structure}

When you program it is always a good idea to structure your code. The main reason for using a consistent set of coding conventions is to standardize the structure and coding style of an application so that you and others can easily read and understand the code. Good coding conventions result in precise, readable, and unambiguous source code that is consistent with other language conventions and as intuitive as possible. 

Have a look at the [guidelines given here](#coding-convention).


## Recap

- Variables are used to store information in a program. The information is saved in memory. A variable may store different data types such as an integer, a double, a string or an object (e.g. a range).
- A selected set of data types are (see all in the [documentation](https://docs.microsoft.com/en-us/office/vba/language/reference/user-interface-help/data-type-summary)):

   Name    | Type      | Details                                                                        
   ------- | --------- | -------------------------------------------------------------------------------
   Byte    | Numerical | Whole number between 0 and 255.                                                
   Integer | Numerical | Whole number between -32'768 and 32'767.                                       
   Long    | Numerical | Whole number between - 2'147'483'648 and 2'147'483'647.                        
   Double  | Numerical | Floating decimal number between -1.79769313486232E308 and 1.79769313486232E308.
   String  | Text      | Text.                                                                          
   Date    | Date      | Date and time.                                                                 
   Boolean | Boolean   | True or False.                                                                 
   Variant | Any type  | Any kind of data (default type if the variable is not declared). 

- Memory consists of bits (binary digit) where a bit can take the value 0 or 1. 
- Common naming of memory chunks are 8 Bits = 1 Byte, 1024 Bytes = 1 Kilobyte, 1024 Kilobytes = 1 Megabyte, 1024 Megabytes = 1 Gigabyte, 1024 Gigabytes = 1 Terabyte.
- Variables take up different storage place in memory based on the data type they use. The size of selected data types:

   Data type                                 | Storage size            
   ----------------------------------------- | ------------------------
   Byte                                      | 1 byte                  
   Boolean                                   | 2 bytes                 
   Integer                                   | 2 bytes                 
   Long  (long integer)                      | 4 bytes                 
   Double  (double-precision floating-point) | 8 bytes                 
   Date                                      | 8 bytes                 
   String                                    | 10 bytes + string length
   Variant  (with numbers)                   | 16 bytes                
   Variant  (with characters)                | 22 bytes + string length

   Arrays require 20 bytes of memory plus 4 bytes for each array dimension plus the number of bytes occupied by the data itself. A Variant containing an array requires 12 bytes more than the array alone.

- Variables must be declared by

  ```
  Dim {variable name} As {data type}
  ```

  Example:
  ```
  Dim intC As Integer		
  intC = 2
  Dim dblCost As Double		
  dblCost = 2.45
  Dim rng As Range			
  Set rng = Range(“A3”)
  ```

  Note the `Set` keyword for setting object variables.

- Declare varibles to optimize memory usage, avoid errors and speed up code (instead of storing in a sheet).
- Use `Option Explicit` in the top of every module so the VBA editor will tell you if you forget to declare variables.
- Variable scope:
   - Global variables are known to the whole module or the whole file
   - Declare global variables in the top of the file.
   - In general try to avoid global variables!
   - Local variables are local to a procedure and only known within that procedure.
   - Declare local variables inside the procedure.
- Strings are special variables with varying length.
- Use the `&` to concatenate strings (glue strings together).
- An empty string is of length zero.
- An array store groups of variables of a specific data type. For example
  ```
  Dim intValues(8) As Integer
  ```
  The variable `intValues` is an array with 9 elements which can be accessed using index 0, 1, 2, ..., 8. 
- The default start index of an array is 0. If you want to start with index 1 then add `Option Base 1` to the top of your module.
- An array can have different dimensions, e.g.
  ```
  Dim intOrderSize(52, 100, 50) As Integer  
  ```
  where indices may be (week, customer, product) number. Let us assume that index start from 1 then we have an array with $52 \cdot 100 \cdot 50$ elements which can be accessed using e.g.

  ```
  intOrderSize(2,10, 20)  ' order size week 2, customer 10, product 20 
  ```
- Dynamic arrays are arrays where the dimension is unknown when they are declared. Use `ReDim` to set the dimension later:
 
   ```
   Dim strPeople() As String
   ...
   n = 8
   ReDim strPeople(n)
   ```

You may also have a look at the [slides for this module](https://bss-osca.github.io/tfa/slides/02-01_vba-datatypes-slides.html).

## Exercises {#ex-vba-datatypes}

`r strExercises`

### Exercise - Flight search {#ex-vba-flight}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-flight-template.xlsm) should be used as a starting point for this exercise. 

The sheet _Data_ contains a set of flights between different destinations. You task is to create a program that can search for matching flights given a set of origins and destinations. 

   (1) Have a look at the results in sheet _Main_. The origin and destinations to search for are given in columns A and B and the search result in columns D, E and F. 
   (2) Try pressing the **Clear Search** button and see what happens. Have a look at the code in the VBA editor for this sub and get an overview. You are not expected to understand all the code.
   (3) Try to finish the first part of the `SearchFlights` sub and store the flights in arrays. Use the debugger to check if the values are stored correctly. 

```{r, hint=TRUE, eval=FALSE, title="Hint 2"}
 For o = 1 To intOrigins
     For i = 0 To intFlights - 1
         If Cells(5 + o, 1) = Origin(i) Then 
             For d = 1 To intDestinations
                 ___
             Next 
         End If
     Next 
 Next 
```
```{r, hint=TRUE, eval=FALSE, title="Hint 1", text="You need nested loops (loop over origins, flights, destinations)"}
```
   
   (4) Try to finish the second part of the `SearchFlights` sub and search for matching origin-destination pairs. Note origins and destinations listed to be searched for are also matching origin-destination pairs if they are not in the same row.
   

### Exercise - Product search {#ex-vba-search-products}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-search-products-template.xlsm) contains a sheet _Products_ with a set of products with product code and price. Cell D1 contains the total number of products. 

   (1) Create a sub `FindProduct` that 
       * Declare two arrays to store the price and product code.
       * Uses `ReDim` to set the size of the arrays.
       * Use a for loop to assign values to the arrays.
       * Use an input box to ask for a product code.
       * Use a for loop to search for the product and output the price in a message box.
   (2) Test you code using different product codes
   (3) Add a button to the Main sheet that run the procedure.
   (4) What happens if you write the product code without capital letters? If your code do not work, have a look at the [`UCase`](https://docs.microsoft.com/en-us/office/vba/language/reference/user-interface-help/ucase-function) function.
   (5) Modify your code so that if the product is not found then "Product not found!" is given in a message box. Hint: the [`Exit sub`](https://docs.microsoft.com/en-us/office/vba/language/reference/user-interface-help/exit-statement) may be useful inside the last for loop.
       

### Exercise - Read arrays {#ex-vba-read-arrays}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-read-arrays-template.xlsm) contains different sheets with numbers to be read into an array. 

   (1) The sheet _1D Array_ contains an index number `i` (column A) and value `v` (column B) also known as a long format. Create an array `ary1D` (with index starting from 0) that store the values `v` in array entry `ary1D(i)`. Assume that the default array value is 10. Hint you need a for loop to iterate though the numbers. Have a look at the unfinished sub `Read1DAry` for further hints.
   (2) The sheet _2D Array_ contains an array in matrix format where a cell in row `r` and column `c` contains the value to be stored in the array entry `ary2D(r, c)`. Create the array and store the values. Note `ary2D` start indexing from 1 and not 0. Have a look at the unfinished sub `Read2DAry` for further hints.
   (3) Modify sub `Read2DAry` so that indexing start from 0 instead of 1, i.e. a cell in row `r` and column `c` contains the value to be stored in the array entry `ary2D(r - 1, c - 1)`.
   (4) The sheet _4D Array_ contains values to be stored in a long format (column A:D indices and column E values). Create an array `ary4D` (with index starting from 0) that store the values. Assume that the default value is 5. Have a look at the unfinished sub `Read4DAry` for further hints.
   
   
### Exercise - Read collections {#ex-vba-read-collections}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-read-collections-template.xlsm) contains different sheets with numbers to be read into a collection.

Sheet _Data no keys_ contains some numbers. Create a procedure `CollNoKeys` that:

   (1) Create a collection `col` and add all the numbers.
   (2) Print the collection in a message box (you may use the function `Coll2Str` here).
   (3) Create another collection and add all the items in `col` with value below 5.

Sheet _Data with keys_ contains some ID numbers and prices for a set of products. Create a procedure `CollKeys` that:

   (1) Create a collection `col` and add all the prices using ID as key.
   (2) Print the price of the product with ID92011 in a message box.
   (3) What happens if you try to print the price of ID92?
   


### Exercise - Job sequencing {#ex-vba-job-seq}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-job-sequence-template.xlsm) should be used as a starting point for this exercise. 

Consider $i = 1,...,n$ jobs that has to be done on a machine and let $c_{ij}$ denote the setup cost of switching from job $i$ to job $j$. Moreover, let $c_{0i}$ denote the setup cost of setting up job $i$ when the machine is idle (index 0). Let $s = (0, s_1, \ldots, s_n)$ denote the sequence of jobs and $C$ the total setup costs, e.g. if $s = (0,1,3,2,6,5,4)$, then $C = c_{01} + c_{13} + c_{32} + c_{26} + c_{65} + c_{54}$. Different algorithms for finding a good strategy minimizing the total setup costs exists. A greedy algorithm is:

<div class="box">
**Step 0**: Select the first job as one with minimal idle setup cost.

**Step 1**: Given current job $i$ select the unscheduled with minimal setup cost. 

**Step 2**: If no unscheduled jobs then stop and output the found job sequence else go to Step 1.
</div>

Often a better algorithm is:

<div class="box">
**Step 0**: For each column $j$ find $\bar{c}_j = min(c_{0j},\ldots,c_{j-1,j},c_{j+1,j},\ldots,c_{nj})$ and define relative setup costs $\hat{c}_{ij} = c_{ij}-\bar{c}_{j}$ (the cost is subtracted the minimum value in that column).

**Step 1**: Call the greedy algorithm using costs $\hat{c}_{ij}$.
</div>

Consider sheet _Main_ that contains the setup costs for a scheduling problem.

   (1) Implement the greedy algorithm in a sub `Greedy` that store the setup costs in an ($n+1 \times n$) 2D array `costs` assuming the setup costs table start in cell A4. Next, call the sub `GreedyAlg(costs)` that takes the costs as input argument. The sub must
       - Store the job sequence in an 1D array.
       - Output the number of jobs in cell C1, the job sequence in cell C2 and the total setup costs in cell F1.
       - Highlight the cells with setup costs used in the sequence.
       
   (2) Implement the better algorithm in a sub `Better` that
       - Find and store the minimum setup costs for each column.
       - Calculate the relative costs.
       - Call the greedy algorithm using the relative costs.
       - Correct the total setup costs by adding the minimum costs.
   (3) Add three buttons to _Main_ that remove the solution, run greedy and run better.
   (4) Try to use different datasets from the sheet _Data_ and paste it into A4 in _Main_.


### Exercise - `ByRef` {#ex-vba-byref}

The [file](https://github.com/bss-osca/tfa/blob/master/vba/vba-byref-template.xlsm) should be used as a starting point for this exercise. 

  (1) Create a procedure `AryToVal` satisfying:
      - Arguments are `ByRef ary As Variant, value As Variant`.
      - All entries in the array are set to `value`. 
      - The procedure work both if the dimension of the array is 1 or 2. You may use `AryDim` to find the dimension.
      
      Test the procedure using the `Main` procedure.
  (2) Create a procedure `AryToValByVal` equal to `AryToVal` except that arguments are `ByVal ary As Variant, value As Variant`. Test the procedure using the `Main` procedure. What is the difference?
  (3) Create a procedure `AryToSeq` satisfying:
      - Arguments are `ByRef ary As Variant, lngFrom As Long, lngTo As Long, Optional lngIdx As Long = 1`.
      - Entries in the array is set to values `lngFrom` to `lngTo`.
      - Use `ReDim` to resize the array.
     
      Test the procedure using the `Main` procedure.
  (4) Create a procedure `AryRead` satisfying:
      - Arguments are `ByRef ary As Variant, ByRef rng As Range, Optional intStartIdx1 As Integer = 1, Optional intStartIdx2 As Integer = 1`
      - Entries in the range `rng` are stored in the array `ary`. 
      - Use `ReDim` to resize the array.
     
      Test the procedure using the `Main` procedure.

```{r links, child="links.md", include=FALSE}
```